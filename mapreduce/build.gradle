apply plugin: "java"
apply plugin: "idea"

group = "com.isuraed.insight"
version = "1.0"

repositories {
    // Standard Maven 
    mavenCentral()
    maven {
        url "https://repository.cloudera.com/artifactory/cloudera-repos/"
    }
}

// Mimic Maven 'provided' configuration, as suggested in GRADLE-784
configurations {
    provided
}
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

ext.hadoopVersion = "2.3.0-mr1-cdh5.0.1"
ext.hbaseVersion = "0.96.1.1-cdh5.0.1"
dependencies {
    provided "org.apache.hadoop:hadoop-client:${hadoopVersion}"
    provided "org.apache.hbase:hbase-client:${hbaseVersion}"

    compile "com.googlecode.json-simple:json-simple:1.1.1"
}

// Java version selection
sourceCompatibility = 1.6
targetCompatibility = 1.6

// Emulate Maven shade plugin with a fat jar.
// http://docs.codehaus.org/display/GRADLE/Cookbook#Cookbook-Creatingafatjar
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}
